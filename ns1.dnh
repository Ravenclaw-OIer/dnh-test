#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nonspell 1"]
#Text["Nonspell 1"]
#include "script/default_system/Default_ShotConst.txt"
let objBoss;
let frame = 0;


@Event{
  alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
      SetScriptResult(1500);
    }
    case (EV_REQUEST_TIMER) {
      SetScriptResult(60);
    }
    case (EV_REQUEST_SPELL_SCORE) {
      SetScriptResult(1000000);
    }
}


@Initialize{
  objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
  ObjEnemy_Regist(objBoss);
  let imgExRumia = GetCurrentScriptDirectory() ~ "ExRumia.png";
  ObjPrim_SetTexture(objBoss, imgExRumia);
  ObjSprite2D_SetSourceRect(objBoss, 64, 1, 127, 64);
  ObjSprite2D_SetDestCenter(objBoss);
  ObjMove_SetDestAtFrame(objBoss, GetCenterX(), GetCenterY(), 60);
  TFinalize;
}


let x1 = -1;
let x2 = -1;
let ofs = -1;
@MainLoop{
  ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
  ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);

  if (frame == 60 || x1 < 0) {  // init here
    x1 = ObjMove_GetX(objBoss);
    x2 = ObjMove_GetX(objBoss);
    ofs = (ofs + 1) % 8;
  }
  if (frame > 60) { // We have finished moving
    if (frame % 15 == 0) {
      Fire(x1, ObjMove_GetY(objBoss), ofs);
      Fire(x2, ObjMove_GetY(objBoss), ofs);
    }
    x1 = x1 - 2;
    x2 = x2 + 2;
  }
  frame++;
  yield;
}


task Fire(x, y, ind) {
  if (ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) {
    return;
  }
  let angleT = GetAngleToPlayer(objBoss) + ind * 5;
  ascent (i in 0..6) {
    CreateShotA1(x, y, 1.5, angleT, DS_BALL_BS_RED + ind, 5);
    angleT = (angleT + 60 ) % 360;
  }
}


task TFinalize {
  while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) {
    yield;
  }
  Obj_Delete(objBoss);
  DeleteShotAll(TYPE_ALL, TYPE_IMMEDIATE);
  SetAutoDeleteObject(true);
  CloseScript(GetOwnScriptID());
  return;
}


function GetCenterX(){
  return GetStgFrameWidth() / 2;
}


function GetCenterY(){
  return GetStgFrameHeight() / 2;
}
